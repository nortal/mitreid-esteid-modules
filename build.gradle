buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
  }
}

plugins {
	id "com.github.hierynomus.license" version "0.15.0" apply false
}

allprojects {
	group = 'com.nortal.oidc'
	version = '0.4.2'
	
	repositories {
		jcenter()
	}
	
	apply from: "${rootDir}/gradle-include/eclipse.gradle"
		
	ext.mitreidVersion = '1.2.6'
	
	ext.p = { type, name, conf='default' ->
		def fullType = ':' + rootProject.name + '-' + type
		[path: fullType + fullType + '-' + name, configuration: conf]
	}
	
	ext.overlayDep = {
    p('overlay', it)
	}
	
	ext.supportDep = {
		p('support', it)
	}
}

subprojects {
	apply plugin: 'maven-publish'
	
  tasks.withType(GenerateModuleMetadata) {
    enabled = false
  }

	publishing {
		if (!project.hasProperty('bintrayUsername')) {
    		ext.bintrayUsername = ''
  		}
  	
	  	if (!project.hasProperty('bintrayPassword')) {
	    	ext.bintrayPassword = ''
	  	}

	  	repositories {
	        maven {
	        	url 'https://api.bintray.com/maven/nortal/mitreid-esteid-modules/mitreid'
	        	//url 'https://api.bintray.com/maven/jimmytheneutrino/maven/mitreid'
	            credentials {
	                username bintrayUsername
	                password bintrayPassword
	            }
	        }
	    }
	}
}

configure(subprojects.findAll {it.name.contains('support')}) {
	apply plugin: 'java'
	apply plugin: 'com.github.hierynomus.license'
  
  license {
	  header rootProject.file('LICENSE_HEADER')
    strictCheck true
	}
}